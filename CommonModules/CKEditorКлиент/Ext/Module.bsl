

// Открывает форму редактирования произвольного многострочного текста.
//
// Параметры:
//  ОповещениеОЗакрытии     - ОписаниеОповещения - содержит описание процедуры, которая будет вызвана 
//                            после закрытия формы ввода текста с теми же параметрами, что и для метода
//                            ПоказатьВводСтроки.
//  МногострочныйТекст      - Строка - произвольный текст, который необходимо отредактировать;
//  Заголовок               - Строка - текст, который необходимо отобразить в заголовке формы.
//
// Пример:
//
//   Оповещение = Новый ОписаниеОповещения("КомментарийЗавершениеВвода", ЭтотОбъект);
//   ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(Оповещение, Элемент.ТекстРедактирования);
//
//   &НаКлиенте
//   Процедура КомментарийЗавершениеВвода(Знач ВведенныйТекст, Знач ДополнительныеПараметры) Экспорт
//      Если ВведенныйТекст = Неопределено Тогда
//		   Возврат;
//   	КонецЕсли;	
//	
//	   Объект.МногострочныйКомментарий = ВведенныйТекст;
//	   Модифицированность = Истина;
//   КонецПроцедуры
//
Процедура ПоказатьФормуРедактирования(Знач ОповещениеОЗакрытии, 
	Знач Текст, Знач Заголовок = Неопределено) Экспорт
	
	Если Заголовок = Неопределено Тогда
		Заголовок = "CKEditor";
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.CKEditor",
		Новый Структура("Текст, Заголовок", Текст, Заголовок),
		,
		Истина,
		,
		,
		ОповещениеОЗакрытии,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// API

#Область API

Функция ПолучитьКонтент(ЭлементHTML) Экспорт
	
	Возврат View(ЭлементHTML).getData();
	
КонецФункции

Процедура УстановитьКонтент(ЭлементHTML, Текст) Экспорт
	
	View(ЭлементHTML).setData(Текст);	
	
КонецПроцедуры

#КонецОбласти

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

Функция View(ЭлементHTML)
	
	Результат = Неопределено;
	
	Если ЭтоWebКлиент() Тогда
		Результат = ЭлементHTML.Документ.defaultView;	
	Иначе
		Результат = ЭлементHTML.Документ.parentWindow;
		Если Результат = Неопределено Тогда
			Результат = ЭлементHTML.Документ.defaultView;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверка на запуск 1С в режиме Web-клиента
//  todo: после внедрия заменить на метод БСП
// 
// Возвращаемое значение:
//  Булево - 
//
Функция ЭтоWebКлиент()
	
	ЭтоWebКлиент = Ложь;

	#Если ВебКлиент Тогда
		ЭтоWebКлиент = Истина;
	#КонецЕсли
	
	Возврат ЭтоWebКлиент;
	
КонецФункции

#КонецОбласти